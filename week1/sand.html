<!DOCTYPE html>
<html>

<title>Abelian sand pile</title>

<script id="vertex-shader2" type="x-shader/x-vertex">
//
// for rendering to texture
//
attribute vec4 vPosition;
attribute vec2 vTexCoord;

varying vec2 fTexCoord;

void main()
{

gl_Position = vPosition;
fTexCoord = vTexCoord;

}
</script>
<script id="fragment-shader2" type="x-shader/x-fragment">

precision highp float;

varying  vec2 fTexCoord;

uniform sampler2D texture;
uniform vec2 placementLoc;
uniform float texWidth;

float color2number(vec4 c) {
  float bs = 256.0;//base
  float r = floor(c.r*bs*bs*bs);
  float g = floor(c.g*bs*bs);
  float b = floor(c.b*bs);
  float val = r+g+b;
  if(c.a < 1.0 || val > bs*bs) {
    return 0.0;
  }
  return val;
}

vec4 number2Color( float n) {
  float bs = 256.0;
  float r = floor(mod(n,bs*bs*bs))/(bs*bs*bs);
  float g = floor(mod(n,bs*bs))/(bs*bs);
  float b = floor(mod(n,bs))/(bs);

  return vec4(r,g,b, 1.0);
}

void main() 
{ 
    float MAX_H = 8.0;
    float d = 1.0/texWidth;
    float x = fTexCoord.x;
    float y = fTexCoord.y;
    vec4 up = texture2D( texture, vec2(x, y-d));
    vec4 dn = texture2D( texture, vec2(x, y+d));
    vec4 l = texture2D( texture, vec2(x+d, y));
    vec4 r = texture2D( texture, vec2(x-d, y));
    vec4 center = texture2D( texture, vec2(x, y));
    float upN = color2number(up);
    float dnN = color2number(dn);
    float lN = color2number(l);
    float rN = color2number(r);
    float me = color2number(center);
    if(me >= MAX_H){//topple pile
      me = me - MAX_H;
    }
    // add toppled neighbors
    if(upN >= MAX_H) {
      me = me + MAX_H/4.0;
    }
    if(dnN >= MAX_H) {
      me = me + MAX_H/4.0;
    }
    if(lN >= MAX_H) {
      me = me + MAX_H/4.0;
    }
    if(rN >= MAX_H) {
      me = me + MAX_H/4.0;
    }
    // add more particles to the middle
    // 
    if( placementLoc.x > 0.0 && placementLoc.y > 0.0){
      if(x > (placementLoc.x-0.6*d) && x < placementLoc.x && y > (placementLoc.y-0.6*d) && y<placementLoc.y){
        me = me + 1.0;
      }
    }
    gl_FragColor = number2Color(me);
} 

</script>




<script id="vertex-shader-display" type="x-shader/x-vertex">
//
// This shader is for displaying to the screen
//
attribute vec4 vPosition;
attribute vec2 vTexCoord;
varying vec2 fTexCoord;
uniform mat4 vProjection;

void main() {
    vec4 bla = vProjection*vPosition; 
    fTexCoord = vTexCoord;
    gl_Position = bla;
} 
</script>
<script id="fragment-shader-display" type="x-shader/x-fragment">
//
//  also for displaying to the screen
//
precision highp float;
uniform sampler2D texture;
varying vec2 fTexCoord;

float color2number(vec4 c) {
  float bs = 256.0;//base
  float r = floor(c.r*bs*bs*bs);
  float g = floor(c.g*bs*bs);
  float b = floor(c.b*bs);
  float val = r+g+b;
  if(c.a < 1.0 || val > bs*bs) {
    return 0.0;
  }
  return val;
}

vec3 hsv2rgb(vec3 c) {
  vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
  vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
  return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

void main() {
    float x = fTexCoord.x;
    float y = fTexCoord.y;
    vec4 center = texture2D( texture, vec2(x, y));
    float me = color2number(center);
    vec3 col2 = hsv2rgb(vec3(0.5+me/16.0, 1.0, me/4.0));
    gl_FragColor = vec4(col2, 1.0);
}
</script>



<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="sand.js"></script>

<body>
zoom: <input type=range min=0.25 max=30 step=0.1 value=3 oninput="projectionMatrix[0][0] = projectionMatrix[1][1] =  Number(this.value)"></input>
speed: <input type=range min=1 max=300 step=1 value=1 oninput="speedMult =  Number(this.value)"></input>

<br>
<canvas id="gl-canvas" width="512" height="512">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
<br>
<a href="https://en.wikipedia.org/wiki/Abelian_sandpile_model">Abelian sand pile</a> calulated in webgl shaders. 
</body>
</html>
